# Maintainer: Ole Ernst <olebowle[at]gmx[dot]com>
pkgname=media-build-dvbsky
pkgver=20130520
pkgrel=1
pkgdesc="Driver for DVBSky cards/boxes"
arch=('i686' 'x86_64')
url="http://www.dvbsky.net/Support.html"
license=('GPL2')
depends=('dvbsky-firmware')
makedepends=('linux-headers' 'perl-proc-processtable')
install="${pkgname}.install"
source=("http://www.dvbsky.net/download/linux/media_build-bst-130520.tar.gz")
md5sums=('3396b2b8cd66f9c7536bb63d526cf5c9')

#sever doesn't like the curl header
DLAGENTS="$( IFS=$'\n'; echo "${DLAGENTS[*]}" | grep '^http::' ) --user-agent 'Mozilla/4.0'"

prepare() {
  cd "${srcdir}/media_build-bst"
  [ "$CARCH" == 'x86_64' ] && cp v4l/sit2_dvbc.o.x64 v4l/sit2.o
  [ "$CARCH" == 'i686' ] && cp v4l/sit2_dvbc.o.x86 v4l/sit2.o
  sed -i -e "s|/sbin/lsmod|$(which lsmod)|" -e 's|/sbin/depmod|#/sbin/depmod|' v4l/Makefile
  sed -i 's|/sbin/depmod|#/sbin/depmod|' v4l/scripts/make_makefile.pl
}

build() {
  cd "${srcdir}/media_build-bst"
  #media-build doesn't like parallel jobs
  make -j1
}

package() {
  cd "${srcdir}/media_build-bst"
  make DESTDIR="${pkgdir}" KDIR26="/usr/lib/modules/$(uname -r)/updates/kernel/drivers/media" media-install
  find "${pkgdir}" -type f -name '*.ko' -exec gzip -9 {} \;
}
