# Maintainer: Ole Ernst <olebowle[at]gmx[dot]com>
pkgname=media-build-dvbsky
pkgver=20140926
pkgrel=1
pkgdesc="Driver for DVBSky cards/boxes"
arch=('i686' 'x86_64')
url="http://www.dvbsky.net/Support.html"
license=('GPL2')
depends=('dvbsky-firmware')
makedepends=('linux-headers' 'perl-proc-processtable')
conflicts=('dvbsky-dvb-drivers')
replaces=('dvbsky-dvb-drivers')
install="${pkgname}.install"
source=("http://dvbsky.net/download/linux/media_build-bst-14-140926.tar.gz")
md5sums=('eb35e60d1f8f9df8818a7c37e0bf3189')

#server doesn't like curl' user agent
DLAGENTS="$( IFS=$'\n'; echo "${DLAGENTS[*]}" | grep '^http::' ) --user-agent 'Mozilla/4.0'"

prepare() {
  cd "${srcdir}/media_build-bst-14/v4l"
  ln -s sit2_op.o.x${CARCH: -2} sit2_op.o
  cp sit2_mod.dvb ../linux/drivers/media/dvb-frontends/sit2_mod.c
  sed -i -e "s|/sbin/lsmod|$(which lsmod)|" -e 's|/sbin/depmod|#/sbin/depmod|' Makefile
  sed -i 's|/sbin/depmod|#/sbin/depmod|' scripts/make_makefile.pl
}

build() {
  cd "${srcdir}/media_build-bst-14"
  #media-build doesn't like parallel jobs
  make -j1
}

package() {
  cd "${srcdir}/media_build-bst-14"
  make DESTDIR="${pkgdir}" KDIR26="/usr/lib/modules/$(uname -r)/updates/kernel/drivers/media" media-install
  find "${pkgdir}" -type f -name '*.ko' -exec gzip -9 {} \;
}
