# Maintainer: Ole Ernst <olebowle[at]gmx[dot]com>
pkgname=media-build-dvbsky
pkgver=20140601
pkgrel=1
pkgdesc="Driver for DVBSky cards/boxes"
arch=('i686' 'x86_64')
url="http://www.dvbsky.net/Support.html"
license=('GPL2')
depends=('dvbsky-firmware')
makedepends=('linux-headers' 'perl-proc-processtable')
conflicts=('dvbsky-dvb-drivers')
replaces=('dvbsky-dvb-drivers')
install="${pkgname}.install"
source=("http://dvbsky.net/download/linux/media_build-bst-14-140601.tar.gz")
md5sums=('eb2740866fcf409f3647d41052d9fe14')

#server doesn't like curl' user agent
DLAGENTS="$( IFS=$'\n'; echo "${DLAGENTS[*]}" | grep '^http::' ) --user-agent 'Mozilla/4.0'"

prepare() {
  cd "${srcdir}/media_build-bst-14/v4l"
  ln -s sit2.o.x${CARCH: -2} sit2.o
  sed -i -e "s|/sbin/lsmod|$(which lsmod)|" -e 's|/sbin/depmod|#/sbin/depmod|' Makefile
  sed -i 's|/sbin/depmod|#/sbin/depmod|' scripts/make_makefile.pl
  sed -i '/__DATE__/d' ../linux/drivers/media/usb/dvb-usb-v2/dvbsky.c
}

build() {
  cd "${srcdir}/media_build-bst-14"
  #media-build doesn't like parallel jobs
  make -j1
}

package() {
  cd "${srcdir}/media_build-bst-14"
  make DESTDIR="${pkgdir}" KDIR26="/usr/lib/modules/$(uname -r)/updates/kernel/drivers/media" media-install
  find "${pkgdir}" -type f -name '*.ko' -exec gzip -9 {} \;
}
