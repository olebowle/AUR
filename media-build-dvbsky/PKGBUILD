# Maintainer: Ole Ernst <olebowle[at]gmx[dot]com>
pkgname=media-build-dvbsky
pkgver=20140128
pkgrel=2
pkgdesc="Driver for DVBSky cards/boxes"
arch=('i686' 'x86_64')
url="http://www.dvbsky.net/Support.html"
license=('GPL2')
depends=('dvbsky-firmware')
makedepends=('linux-headers' 'perl-proc-processtable')
install="${pkgname}.install"
source=("http://www.dvbsky.net/download/linux/media_build-bst-140128.tar.gz")
md5sums=('c538c1635e7f9838f1c56409943f7080')

#sever doesn't like the curl header
DLAGENTS="$( IFS=$'\n'; echo "${DLAGENTS[*]}" | grep '^http::' ) --user-agent 'Mozilla/4.0'"

prepare() {
  cd "${srcdir}/media_build-bst/v4l"
  ln -s sit2.o.x${CARCH: -2} sit2.o
  sed -i -e "s|/sbin/lsmod|$(which lsmod)|" -e 's|/sbin/depmod|#/sbin/depmod|' Makefile
  sed -i 's|/sbin/depmod|#/sbin/depmod|' scripts/make_makefile.pl
  sed -i '/__DATE__/d' ../linux/drivers/media/usb/dvb-usb-v2/dvbsky.c
}

build() {
  cd "${srcdir}/media_build-bst"
  #media-build doesn't like parallel jobs
  make -j1
}

package() {
  cd "${srcdir}/media_build-bst"
  make DESTDIR="${pkgdir}" KDIR26="/usr/lib/modules/$(uname -r)/updates/kernel/drivers/media" media-install
  find "${pkgdir}" -type f -name '*.ko' -exec gzip -9 {} \;
}
