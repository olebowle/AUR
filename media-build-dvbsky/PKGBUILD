# Maintainer: Ole Ernst <olebowle[at]gmx[dot]com>
pkgname=media-build-dvbsky
pkgver=20130407
pkgrel=1
pkgdesc="Driver for DVBSky cards/boxes"
arch=('i686' 'x86_64')
url="http://www.dvbsky.net/Support.html"
license=('GPL2')
depends=('dvbsky-firmware')
makedepends=('linux-headers' 'perl-proc-processtable')
install="${pkgname}.install"
source=("http://www.dvbsky.net/download/linux/media_build-bst-130407-experimental.tar.gz")
md5sums=('713cfce8696f21903acb0e4c729f34e8')

#sever doesn't like the curl header
DLAGENTS="$( IFS=$'\n'; echo "${DLAGENTS[*]}" | grep '^http::' ) --user-agent 'Mozilla/4.0'"

prepare() {
  cd "${srcdir}/media_build-bst"
  sed -i -e "s|/sbin/lsmod|$(which lsmod)|" -e 's|/sbin/depmod|#/sbin/depmod|' v4l/Makefile
  sed -i 's|/sbin/depmod|#/sbin/depmod|' v4l/scripts/make_makefile.pl
}

build() {
  cd "${srcdir}/media_build-bst"
  #media-build doesn't like parallel jobs
  make -j1
}

package() {
  cd "${srcdir}/media_build-bst"
  make DESTDIR="${pkgdir}" KDIR26="/usr/lib/modules/$(uname -r)/updates/kernel/drivers/media" media-install
  find "${pkgdir}" -type f -name '*.ko' -exec gzip -9 {} \;
}
