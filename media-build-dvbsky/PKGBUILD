# Maintainer: Ole Ernst <olebowle[at]gmx[dot]com>
pkgname=media-build-dvbsky
pkgver=20130128
pkgrel=1
pkgdesc="Driver for DVBSky cards/boxes"
url="http://www.dvbsky.net/Support.html"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('dvbsky-firmware')
makedepends=('linux-headers' 'perl-proc-processtable')
install="${pkgname}.install"
source=("http://www.dvbsky.net/download/media_build-bst-130126-experimental.tar.gz")
md5sums=('41df4bf74107f17dd632777b11d07c9b')

#sever doesn't like the curl header
DLAGENTS="$( IFS=$'\n'; echo "${DLAGENTS[*]}" | grep '^http::' ) --user-agent 'Mozilla/4.0'"

build() {
  cd "${srcdir}/media_build-bst-130126"
  sed -i -e "s|/sbin/lsmod|$(which lsmod)|" -e "s|/sbin/depmod|#/sbin/depmod|" v4l/Makefile
  sed -i "s|/sbin/depmod|#/sbin/depmod|" v4l/scripts/make_makefile.pl
  #media-build doesn't like parallel jobs
  MAKEFLAGS="-j1" make
}

package() {
  cd "${srcdir}/media_build-bst-130126"
  make DESTDIR="${pkgdir}" KDIR26="/usr/lib/modules/$(uname -r)/updates/kernel/drivers/media" media-install
  find "${pkgdir}" -name '*.ko' -exec gzip -9 {} \;
}
