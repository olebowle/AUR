# Maintainer: Ole Ernst <olebowle[at]gmx[dot]com>
pkgname=media-build-dvbsky
pkgver=20130315
pkgrel=1
pkgdesc="Driver for DVBSky cards/boxes"
url="http://www.dvbsky.net/Support.html"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('dvbsky-firmware')
makedepends=('linux-headers' 'perl-proc-processtable')
install="${pkgname}.install"
source=("http://www.dvbsky.net/download/media_build-bst-130315-experimental.tar.gz"
        'cx23885-core.patch')
md5sums=('9ee5ab78d94a494a5fc97181d9bc430a'
         'ddefc9dbfc4282d2ffb221eff8e5c6e1')

#sever doesn't like the curl header
DLAGENTS="$( IFS=$'\n'; echo "${DLAGENTS[*]}" | grep '^http::' ) --user-agent 'Mozilla/4.0'"

build() {
  cd "${srcdir}/media_build-bst"
  sed -i -e "s|/sbin/lsmod|$(which lsmod)|" -e "s|/sbin/depmod|#/sbin/depmod|" v4l/Makefile
  sed -i "s|/sbin/depmod|#/sbin/depmod|" v4l/scripts/make_makefile.pl
  patch -p1 -i "${srcdir}/cx23885-core.patch"
  #media-build doesn't like parallel jobs
  MAKEFLAGS="-j1" make
}

package() {
  cd "${srcdir}/media_build-bst"
  make DESTDIR="${pkgdir}" KDIR26="/usr/lib/modules/$(uname -r)/updates/kernel/drivers/media" media-install
  find "${pkgdir}" -name '*.ko' -exec gzip -9 {} \;
}
