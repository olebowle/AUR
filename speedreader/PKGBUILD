# Maintainer: Ole Ernst <olebowle[at]gmx[dot]com>
_srcname=SpeedReader
pkgname=${_srcname,,}
pkgver=1.3.0
pkgrel=4
pkgdesc="Cross-platform speed reading developed with Qt"
url="http://www.vanniktech.de/${_srcname}"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('libxkbcommon-x11' 'qt5-base')
makedepends=('gendesk')
source=("https://github.com/vanniktech/${_srcname}/archive/v${pkgver}.tar.gz")
md5sums=('8ad52954c9e571d1e482df135f1004da')

prepare() {
  gendesk -n --pkgname "${pkgname}" --pkgdesc "${pkgdesc}" --categories "Education;Viewer;TextTools;News"
}

build() {
  cd "${_srcname}-${pkgver}"
  qmake-qt5 ${_srcname}.pro -r -spec linux-g++
  make
}

package() {
  cd "${_srcname}-${pkgver}"
  install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm755 ${_srcname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 icon.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
}
